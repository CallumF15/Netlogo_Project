;; navmesh structure: [ [<patch> ...] ...]
;; navmesh-matrix structure: [ [<adjacent-node> ...] ...]

globals [
  navmesh
  navmesh-matrix
]

to setup-navmesh
  set navmesh []
  set navmesh-matrix []

  ;; generate navmesh and adjacency matrix
  let x 0
  repeat max-pxcor + 1 [
    let vertical-slab (list sort patches with [ pxcor = x ])
    let y max-pycor
    let segment-index 0

    ;; scan vertical slab
    foreach first vertical-slab [
      ask ? [
        if pcolor = black [
          ;; make new segment with patches below obstacle
          let new-segment filter [ [ pycor ] of ? < y ] last vertical-slab
          if not empty? new-segment [
            set vertical-slab lput new-segment vertical-slab
          ]

          ;; remove obstacle and patches below it from segment
          set vertical-slab replace-item segment-index vertical-slab filter [ [ pycor ] of ? > y ] item segment-index vertical-slab
          if empty? item segment-index vertical-slab [
            set vertical-slab remove-item segment-index vertical-slab
          ]

          set segment-index length vertical-slab - 1
        ]
      ]

      set y y - 1
    ]

    ifelse x > 0 [
      foreach vertical-slab [
        let segment ?
        
        let top-edge [ pycor ] of first segment
        let bottom-edge [ pycor ] of last segment
        let left-edge [ pxcor ] of first segment

        let unioned? false
        let neighboring-nodes filter [ [ pxcor ] of last ? + 1 = left-edge ] navmesh

        ;; union segments
        let union-node filter [ [ pycor ] of first ? = top-edge and [ pycor ] of last ? = bottom-edge ] neighboring-nodes
        if not empty? union-node [
          set union-node first union-node
          
          ;; add segment to union-node
          set navmesh replace-item (position union-node navmesh) navmesh sort sentence union-node segment

          ;; remove segment from vertical-slab
          set vertical-slab remove segment vertical-slab

          set unioned? true
        ]

        if not unioned? [
          set navmesh-matrix lput [] navmesh-matrix
          set segment-index length navmesh-matrix - 1

          ;; add entiries to adjacency matrix
          let adjacent-nodes filter [ [ pycor ] of first ? >= bottom-edge and [ pycor ] of last ? <= top-edge ] neighboring-nodes
          foreach adjacent-nodes [
            let node-index position ? navmesh

            set navmesh-matrix replace-item node-index navmesh-matrix lput segment-index item node-index navmesh-matrix
            set navmesh-matrix replace-item segment-index navmesh-matrix lput node-index item segment-index navmesh-matrix
          ]
        ]
      ]
    ] [
      foreach vertical-slab [
        set navmesh-matrix lput [] navmesh-matrix
      ]
    ]

    ;; add segments to navmesh
    foreach vertical-slab [
      set navmesh lput ? navmesh
    ]

    set x x + 1
  ]

  update-navmesh-display
end



to-report get-centre [ #node ]
  ifelse #node < 0 or #node >= length navmesh [
    report []
  ] [
    let x 0
    let y 0
    
    foreach item #node navmesh [
      set x x + [ pxcor ] of ?
      set y y + [ pycor ] of ?
    ]
    
    report map [ ? / length item #node navmesh ] (list x y)
  ]
end



to-report get-edge [ #node-1 #node-2 ]
  ifelse #node-1 < 0 or #node-1 >= length navmesh or #node-2 < 0 or #node-2 >= length navmesh [
    report [ -1 -1 ]
  ] [
    let node-1 item #node-1 navmesh
    let node-2 item #node-2 navmesh
    
    let left-edge-1 filter [ [ pxcor ] of ? = [ pxcor ] of first node-1 ] node-1
    let right-edge-1 filter [ [ pxcor ] of ? = [ pxcor ] of last node-1 ] node-1
    
    let left-edge-2 filter [ [ pxcor ] of ? = [ pxcor ] of first node-2 ] node-2
    let right-edge-2 filter [ [ pxcor ] of ? = [ pxcor ] of last node-2 ] node-2
    
    let x []
    let shared-edge []
    
    ifelse [ pxcor ] of first left-edge-1 > [ pxcor ] of first right-edge-2 [
      ;; node-1 is to the right of node-2
      set x ([ pxcor ] of first left-edge-1 + 0.5 + [ pxcor ] of first right-edge-2 - 0.5) / 2
      set shared-edge (list left-edge-1 right-edge-2)
    ] [
      ;; node-2 is to the right of node-1
      set x ([ pxcor ] of first left-edge-2 + 0.5 + [ pxcor ] of first right-edge-1 - 0.5) / 2
      set shared-edge (list left-edge-2 right-edge-1)
    ]
    
    let ys []
    
    foreach first shared-edge [
      let y1 [ pycor ] of ?
      
      foreach last shared-edge [
        let y2 [ pycor ] of ?
        
        if y1 = y2 [
          set ys lput y1 ys
        ]
      ]
    ]
    
    set ys sort ys
    let y (list (first ys - 0.5) (last ys + 0.5))
    
    report (list (list x first y) (list x last y))
  ]
end




to-report containing-node [ #patch ]
  ifelse not is-patch? #patch [
    report -1
  ] [
    let index 0
    
    foreach navmesh [
      if member? #patch ? [
        report index
      ]
      
      set index index + 1
    ]
    
    report -1
  ]
end



to-report adjacency-list [ #node ]
  ifelse #node < 0 or #node >= length navmesh-matrix [
    report []
  ] [
    report item #node navmesh-matrix
  ]
end
